apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties projects = new Properties()
projects.load(project.rootProject.file('project.properties').newDataInputStream())

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging projects['project.packaging']
                name projects['project.description']  //项目描述
                url projects['project.siteUrl']

                groupId projects['project.groupId']
                artifactId projects['project.artifactId']
                version projects['project.version']

                licenses {
                    license {
                        name projects['project.licenseName']
                        url projects['project.licenseUrl']
                    }
                }
                developers {
                    developer {
                        name properties['developer.name']  //填写开发者基本信息
                        email properties['developer.email']
                    }
                }
                scm {
                    connection projects['project.gitUrl']
                    developerConnection projects['project.gitUrl']
                    url projects['project.siteUrl']
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    options.encoding = "utf-8"
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = properties['bintray.user']
    key = properties['bintray.apikey']
    configurations = ['archives']
    pkg {
        repo = projects['project.repo']  //发布到Bintray的那个仓库里，默认账户有四个库，我们这里上传到maven库
        name = projects['project.name']  //发布到Bintray上的项目名字
        websiteUrl = projects['project.siteUrl']
        vcsUrl = projects['project.gitUrl']
        licenses = projects['project.allLicenses']
        publish = true
    }
}

